plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'fabric-loom' version '1.9-SNAPSHOT' apply false
	id 'ploceus' version '1.9-SNAPSHOT' apply false
}

setUpJar(project)

publishing {
	publications {
		mavenJava(MavenPublication) {
			pom.withXml {
				asNode().appendNode('dependencies')
			}
		}
	}
	repositories {
		def ENV = System.getenv()

		if (ENV.MAVEN_URL) {
			maven {
				url ENV.MAVEN_URL

				credentials {
					username ENV.MAVEN_USERNAME
					password ENV.MAVEN_PASSWORD
				}
			}
		} else {
			mavenLocal()
		}
	}
}

def setUpJar(project) {
	project.apply plugin: 'java-library'
	project.apply plugin: 'eclipse'
	project.apply plugin: 'idea'
	project.apply plugin: 'fabric-loom'
	project.apply plugin: 'ploceus'

	project.base {
		archivesName = "${project.rootProject.root_archives_base_name}"
	}
	project.version = "${project.rootProject.root_version}"
	project.group = "${project.rootProject.root_maven_group}"

	project.ploceus {
		setGeneration(2)
	}

	project.repositories {
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
	}

	project.dependencies {
		// dummy mc dependency to keep loom happy
		minecraft "com.mojang:minecraft:${project.rootProject.minecraft_version}"
		mappings project.ploceus.featherMappings(project.rootProject.feather_build)
		modImplementation "net.fabricmc:fabric-loader:${project.rootProject.loader_version}"
	}

	project.processResources {
		filesMatching('fabric.mod.json') {
			expand project.properties
		}
	}

	project.jar {
		from(project.rootProject.file('LICENSE')) {
			rename { "${it}_${project.base.archivesName.get()}" }
		}
	}
}

def addPomDependency(project) {
	project.rootProject.publishing {
		publications {
			mavenJava(MavenPublication) {
				pom.withXml {
					def depsNode = asNode().get("dependencies")[0]
					def depNode = depsNode.appendNode("dependency")

					depNode.appendNode("groupId", project.group)
					depNode.appendNode("artifactId", project.name)
					depNode.appendNode("version", project.version)
					depNode.appendNode("scope", "compile")
				}
			}
		}
	}
}

def setUpJavadoc(project) {
	project.javadoc {
		options {
			source = "8"
			encoding = "UTF-8"
			charSet = "UTF-8"
			memberLevel = JavadocMemberLevel.PACKAGE
			// Disable the crazy super-strict doclint tool in Java 8
			addStringOption("Xdoclint:none", "-quiet")

			tags(
				'apiNote:a:API Note:',
				'implSpec:a:Implementation Requirements:',
				'implNote:a:Implementation Note:'
			)
		}

		failOnError true

		include("**/api/**")
		source(project.sourceSets.main.allJava)
	}
}

def setUpLibrary(project) {
	project.apply plugin: 'java-library'
	project.apply plugin: 'eclipse'
	project.apply plugin: 'idea'

	project.base {
		archivesName = "${project.rootProject.archives_base_name}-${project.archives_base_name}"
	}
	project.version = "${project.version}"
	project.group = "${project.rootProject.root_maven_group}.${project.rootProject.maven_group}"

	project.repositories {
		// needed for loader in subprojects without a mc dep
		// since in those subprojects loom is not applied
		maven {
			name = 'Fabric'
			url = 'https://maven.fabricmc.net/'
		}
		maven {
			name = 'Quilt'
			url = 'https://maven.quiltmc.org/repository/release'
		}
		mavenCentral()
	}

	project.dependencies {
		implementation "net.fabricmc:fabric-loader:${project.rootProject.loader_version}"

		implementation "org.apache.logging.log4j:log4j-api:2.19.0"
		implementation "org.apache.logging.log4j:log4j-core:2.19.0"

		implementation project.dependencies.project(path: ':libraries:core', configuration: 'namedElements')
	}

	addPomDependency(project)
	setUpJavadoc(project)
}

def setUpModule(project, String... dependencies) {
	// normally modules have a 'root' and subprojects
	// for different mc version ranges
	// the Core API is the only exception to this
	def isCore = (project.path == ':libraries:core')

	project.apply plugin: 'java-library'
	project.apply plugin: 'eclipse'
	project.apply plugin: 'idea'
	project.apply plugin: 'maven-publish'
	project.apply plugin: 'fabric-loom'
	project.apply plugin: 'ploceus'

	if (isCore) {
		project.base {
			archivesName = "${project.rootProject.archives_base_name}-${project.archives_base_name}"
		}
		project.version = "${project.version}"
	} else {
		project.base {
			archivesName = "${project.rootProject.archives_base_name}-${project.parent.archives_base_name}"
		}
		project.version = "${project.parent.version}+mc${project.min_mc_version}-mc${project.max_mc_version}"
	}
	project.group = "${project.rootProject.root_maven_group}.${project.rootProject.maven_group}"

	project.loom {
		if (!isCore && project.environment == 'client') {
			clientOnlyMinecraftJar()
		}
		if (!isCore && project.environment == 'server') {
			serverOnlyMinecraftJar()
		}
	}
	project.ploceus {
		setGeneration(2)
	}

	def libraries = getLibraryDependencies(project, dependencies)
	def modules = getModuleDependencies(project, dependencies)

	project.repositories {
	}

	project.dependencies {
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings project.ploceus.featherMappings(project.feather_build)
		modImplementation "net.fabricmc:fabric-loader:${project.rootProject.loader_version}"

		if (project.hasProperty('raven_build')) {
			exceptions project.ploceus.raven(project.raven_build)
		}
		if (project.hasProperty('client_raven_build')) {
			clientExceptions project.ploceus.raven(project.client_raven_build, 'client')
		}
		if (project.hasProperty('server_raven_build')) {
			serverExceptions project.ploceus.raven(project.server_raven_build, 'server')
		}
		if (project.hasProperty('sparrow_build')) {
			signatures project.ploceus.sparrow(project.sparrow_build)
		}
		if (project.hasProperty('client_sparrow_build')) {
			clientSignatures project.ploceus.sparrow(project.client_sparrow_build, 'client')
		}
		if (project.hasProperty('server_sparrow_build')) {
			serverSignatures project.ploceus.sparrow(project.server_sparrow_build, 'server')
		}
		if (project.hasProperty('nests_build')) {
			nests project.ploceus.nests(project.nests_build)
		}
		if (project.hasProperty('client_nests_build')) {
			clientNests project.ploceus.nests(project.client_nests_build, 'client')
		}
		if (project.hasProperty('server_nests_build')) {
			serverNests project.ploceus.nests(project.server_nests_build, 'server')
		}

		implementation "org.quiltmc.parsers:json:${project.rootProject.quilt_parsers_version}"
		if (isCore) {
			include "org.quiltmc.parsers:json:${project.rootProject.quilt_parsers_version}"
		}

		libraries.each { libraryPath ->
			implementation project.dependencies.project(path: libraryPath)
		}
		modules.each { modulePath ->
			implementation project.dependencies.project(path: modulePath, configuration: 'namedElements')
		}

		// temporary fix
		// Loom's library processor manager does not run on server-only
		// projects for Minecraft versions without bundle metadata
		if (!isCore && project.environment == 'server') {
			implementation 'org.slf4j:slf4j-api:2.0.1'
			implementation 'org.apache.logging.log4j:log4j-slf4j2-impl:2.19.0'
			implementation 'org.apache.logging.log4j:log4j-api:2.19.0'
			implementation 'org.apache.logging.log4j:log4j-core:2.19.0'
		}
	}

	project.rootProject.dependencies {
		include project.dependencies.project(path: project.path, configuration: 'namedElements')
	}

	project.sourceSets {
		main {
			java {
				if (!isCore) {
					srcDirs += project.parent.sourceSets.main.java.srcDirs
				}
			}
			resources {
				if (!isCore) {
					srcDirs += project.parent.sourceSets.main.resources.srcDirs
				}
			}
		}
	}

	project.processResources {
		filesMatching('fabric.mod.json') {
			expand project.properties
		}
	}

	project.tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = 8
	}

	project.java {
		// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
		// If you remove this line, sources will not be generated.
		withSourcesJar()

		// If this mod is going to be a library, then it should also generate Javadocs in order to aid with development.
		// Uncomment this line to generate them.
		withJavadocJar()
	}

	project.jar {
		from(project.rootProject.file('LICENSE')) {
			rename { "${it}_${project.base.archivesName.get()}" }
		}
	}

	project.publishing {
		publications {
			mavenJava(MavenPublication) {
				if (!isCore) {
					artifactId project.parent.name
				}

				from project.components.java
			}
		}
		repositories {
			def ENV = System.getenv()

			if (ENV.MAVEN_URL && shouldPublish(project, ENV.MAVEN_URL)) {
				maven {
					url ENV.MAVEN_URL

					credentials {
						username ENV.MAVEN_USERNAME
						password ENV.MAVEN_PASSWORD
					}
				}
			} else {
				mavenLocal()
			}
		}
	}

	if (isCore) {
		addPomDependency(project)
	}

	setUpJavadoc(project)
}

def getLibraryDependencies(project, String... moduleNames) {
	def libraries = new ArrayList<>()

	if (project.path != ':libraries:core') {
		libraries.add(project.parent.path)
	}

	for (moduleName : moduleNames) {
		def i = moduleName.indexOf('-mc')
		def libraryName = moduleName.substring(0, i)

		libraries.add(":libraries:${libraryName}")
	}

	return libraries
}

def getModuleDependencies(project, String... moduleNames) {
	def modules = new ArrayList<>()

	if (project.path != ':libraries:core') {
		modules.add(':libraries:core')
	}

	for (moduleName : moduleNames) {
		def i = moduleName.indexOf('-mc')
		def libraryName = moduleName.substring(0, i)

		modules.add(":libraries:${libraryName}:${moduleName}")
	}

	return modules
}

def shouldPublish(project, mavenUrl) {
	def groupId = project.publishing.publications.mavenJava.groupId
	def artifactId = project.publishing.publications.mavenJava.artifactId
	def version = project.publishing.publications.mavenJava.version

	def exists = false

	try {
		def xml = new URL("${mavenUrl}/${groupId.replace('.', '/')}/${artifactId}/maven-metadata.xml").text
		def metadata = new XmlSlurper().parseText(xml)

		exists = metadata.versioning.versions.version.stream().filter({
			return it == version
		}).findFirst().isPresent()
	} catch (FileNotFoundException ignored) {
		// only happens if no publications exist yet
	}

	return !exists
}

allprojects {
	tasks.withType(GenerateModuleMetadata) {
		enabled = false
	}

	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = 'UTF-8'
		it.options.release = 8
	}
}
